MODULE MODE_POSNAM_PHY
IMPLICIT NONE
PRIVATE
PUBLIC :: POSNAM_PHY
CONTAINS
SUBROUTINE POSNAM_PHY(TFILENAM, CDNAML, LDNEEDNAM, LDFOUND)

!To position namelist file at correct place for reading namelists
!Code adapted from different sources (ECMWF, ARPIFS, MESO-NH)

USE MODE_MSG, ONLY: NVERB_FATAL, NVERB_WARNING, PRINT_MSG
USE MODD_IO, ONLY: TFILEDATA

IMPLICIT NONE

TYPE(TFILEDATA),  INTENT(IN)    :: TFILENAM  !< Namelist file
CHARACTER(LEN=*), INTENT(IN)    :: CDNAML    !< Namelist name
LOGICAL,          INTENT(IN)    :: LDNEEDNAM !< True to abort if namelist is absent
LOGICAL,          INTENT(OUT)   :: LDFOUND   !< True if namelist has been found

INTEGER :: IVERB, ILEN, ISTATUS, ISCAN, IND
CHARACTER(LEN=120) :: CLINE
CHARACTER(LEN=1)   ::  CLTEST

REWIND(TFILENAM%NLU)
ILEN=LEN(CDNAML)
LDFOUND=.TRUE.
ISTATUS=0
ISCAN=0
CLINE=' '
DO WHILE(ISTATUS==0 .AND. ISCAN==0)
  READ(TFILENAM%NLU,'(A)',IOSTAT=ISTATUS) CLINE
  IF(ISTATUS<=-1) THEN
    !End of file
    LDFOUND=.FALSE.
    IF(LDNEEDNAM) THEN
      IVERB=NVERB_FATAL
    ELSE
      IVERB=NVERB_WARNING
    ENDIF
    CALL PRINT_MSG(IVERB, 'GEN', 'POSNAM_PHY', 'CANNOT LOCATE '//CDNAML)
  ELSEIF(ISTATUS>=1) THEN
    !Error
    CALL PRINT_MSG(NVERB_FATAL, 'GEN', 'POSNAM_PHY', 'AN ERROR HAPPENED WHILE READING THE NAMELIST')
  ELSE !ISTATUS==0
    !Normal line
    IND=INDEX(TO_UPPER(CLINE), '&'//TO_UPPER(CDNAML))
    IF(IND/=0) THEN
      CLTEST=CLINE(IND+ILEN+1:IND+ILEN+1)
      IF (.NOT. ((LGE(CLTEST,'0').AND.LLE(CLTEST,'9')) .OR. &
                &(LGE(CLTEST,'A').AND.LLE(CLTEST,'Z')))) THEN
        ISCAN=1
      ENDIF
    ENDIF
  ENDIF
ENDDO
BACKSPACE(TFILENAM%NLU)

END SUBROUTINE POSNAM_PHY

PURE FUNCTION TO_UPPER(CSTRING) RESULT(CUPPER_STRING)
  IMPLICIT NONE
  CHARACTER(LEN=*), INTENT(IN) :: CSTRING       !< Input string to be put in upper case
  CHARACTER(LEN=LEN(CSTRING))  :: CUPPER_STRING !< Resulting string
  CHARACTER(LEN=26), PARAMETER :: UPPER_CHAR = 'ABCDEFGHIJKLMNOPQRSTUVWXYZ'
  CHARACTER(LEN=26), PARAMETER :: LOWER_CHAR = 'abcdefghijklmnopqrstuvwxyz'
  INTEGER :: II, IX
  CUPPER_STRING=CSTRING
  DO II=1, LEN(CUPPER_STRING)
     IX=INDEX(LOWER_CHAR, CUPPER_STRING(II:II))
     IF(IX/= 0) CUPPER_STRING(II:II)=UPPER_CHAR(IX:IX)
  END DO    
END FUNCTION TO_UPPER
END MODULE MODE_POSNAM_PHY
