SUBROUTINE INI_PHYEX(HPROGRAM, KUNITNML, LDNEEDNAM, KLUOUT, KFROM, KTO, &
                    &PTSTEP, PDZMIN, &
                    &CMICRO, &
                    &LDCHANGEMODEL, LDDEFAULTVAL, LDREADNAM, LDCHECK, LDPRINT, LDINIT, &
                    &PARAM_ICE_INOUT, RAIN_ICE_DESCR_INOUT, RAIN_ICE_PARAM_INOUT, CLOUDPARN_INOUT)
!
USE MODD_PARAM_ICE, ONLY: PARAM_ICE_ASSOCIATE, PARAM_ICE_INIT, PARAM_ICE, PARAM_ICE_t
USE MODD_RAIN_ICE_DESCR, ONLY: RAIN_ICE_DESCR, RAIN_ICE_DESCR_t
USE MODD_RAIN_ICE_PARAM, ONLY: RAIN_ICE_PARAM, RAIN_ICE_PARAM_t
USE MODD_CLOUDPAR_N,     ONLY: CLOUDPAR_GOTO_MODEL, CLOUDPARN, CLOUDPAR_t
!
USE MODE_INI_RAIN_ICE, ONLY: INI_RAIN_ICE
USE MODE_INI_TIWMX, ONLY: INI_TIWMX
USE MODE_INI_SNOW, ONLY: INI_SNOW

IMPLICIT NONE

CHARACTER(LEN=6),  INTENT(IN) :: HPROGRAM     !< Current program
INTEGER,           INTENT(IN) :: KUNITNML     !< Logical unit to access the namelist
LOGICAL,           INTENT(IN) :: LDNEEDNAM    !< True to abort if namelist is absent
INTEGER,           INTENT(IN) :: KLUOUT       !< Logical unit for outputs
INTEGER,           INTENT(IN) :: KFROM        !< Old model number
INTEGER,           INTENT(IN) :: KTO          !< New model number
REAL,              INTENT(IN) :: PTSTEP       !< Timestep
REAL,              INTENT(IN) :: PDZMIN       !< Minimum thickness
CHARACTER(4),      INTENT(IN) :: CMICRO       !< Microphysical scheme to use
LOGICAL, OPTIONAL, INTENT(IN) :: LDCHANGEMODEL!< Must we change the active model
LOGICAL, OPTIONAL, INTENT(IN) :: LDDEFAULTVAL !< Must we initialize variables with default values (defaults to .TRUE.)
LOGICAL, OPTIONAL, INTENT(IN) :: LDREADNAM    !< Must we read the namelist (defaults to .TRUE.)
LOGICAL, OPTIONAL, INTENT(IN) :: LDCHECK      !< Must we perform some checks on values (defaults to .TRUE.)
LOGICAL, OPTIONAL, INTENT(IN) :: LDPRINT      !< Must we print the effective values (defaults to .TRUE.)
LOGICAL, OPTIONAL, INTENT(IN) :: LDINIT       !< Must we call the init routines
TYPE(PARAM_ICE_t),       OPTIONAL, INTENT(INOUT) :: PARAM_ICE_INOUT
TYPE(RAIN_ICE_DESCR_t) , OPTIONAL, INTENT(INOUT) :: RAIN_ICE_DESCR_INOUT
TYPE(RAIN_ICE_PARAM_t) , OPTIONAL, INTENT(INOUT) :: RAIN_ICE_PARAM_INOUT
TYPE(CLOUDPAR_t),        OPTIONAL, INTENT(INOUT) :: CLOUDPARN_INOUT

LOGICAL :: LLINIT, LLCHANGEMODEL

LLINIT=.TRUE.
IF(PRESENT(LDINIT)) LLINIT=LDINIT
LLCHANGEMODEL=.TRUE.
IF(PRESENT(LDCHANGEMODEL)) LLCHANGEMODEL=LDCHANGEMODEL

IF(CMICRO=='ICE3' .OR. CMICRO=='ICE4' .OR. CMICRO=='LIMA') THEN
  IF(LLCHANGEMODEL) CALL CLOUDPAR_GOTO_MODEL(KFROM, KTO)
  IF(PRESENT(PARAM_ICE_INOUT)) PARAM_ICE=PARAM_ICE_INOUT
  IF(PRESENT(RAIN_ICE_DESCR_INOUT)) RAIN_ICE_DESCR=RAIN_ICE_DESCR_INOUT
  IF(PRESENT(RAIN_ICE_PARAM_INOUT)) RAIN_ICE_PARAM=RAIN_ICE_PARAM_INOUT
  IF(PRESENT(CLOUDPARN_INOUT)) CLOUDPARN=CLOUDPARN_INOUT

  CALL PARAM_ICE_ASSOCIATE()
  CALL PARAM_ICE_INIT(HPROGRAM, KUNITNML, LDNEEDNAM, KLUOUT, &
                     &LDDEFAULTVAL, LDREADNAM, LDCHECK, LDPRINT)
  IF(LLINIT) THEN
    CALL INI_RAIN_ICE(KLUOUT, PTSTEP, PDZMIN, CLOUDPARN%NSPLITR, CMICRO)
    CALL INI_TIWMX
  
    IF(RAIN_ICE_PARAM%XFRMIN(16) > 0.) THEN
       CALL INI_SNOW(KLUOUT) ! Recalculate snow parameters :  XCCS = XFRMIN(16),XCXS = XFRMIN(17)
    ENDIF
  ENDIF

  IF(PRESENT(PARAM_ICE_INOUT)) PARAM_ICE_INOUT=PARAM_ICE
  IF(PRESENT(RAIN_ICE_DESCR_INOUT)) RAIN_ICE_DESCR_INOUT=RAIN_ICE_DESCR
  IF(PRESENT(RAIN_ICE_PARAM_INOUT)) RAIN_ICE_PARAM_INOUT=RAIN_ICE_PARAM
  IF(PRESENT(CLOUDPARN_INOUT)) CLOUDPARN_INOUT=CLOUDPARN
ENDIF

END SUBROUTINE INI_PHYEX
