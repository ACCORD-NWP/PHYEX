!!    ##############################
SUBROUTINE AROINI_NSV0(OUSECHEM,OORILAM,ODUST,OCO2,ODEPOS,KGFL,KGFL_EXT,&
                      KSV_CHEMBEG, KSV_CHEMEND, KSV_DSTBEG, KSV_DSTEND, &
                      KSV_DSTDEPBEG, KSV_DSTDEPEND,                     &
                      KSV_AERBEG, KSV_AEREND, KSV_CO2, CLNAME)
USE PARKIND1, ONLY : JPRB
USE YOMHOOK , ONLY : LHOOK, DR_HOOK
!!    ##############################

! MesoNH-C modules 
USE MODD_CH_M9 ,       ONLY : NEQ, CNAMES
USE MODD_CH_M9_SCHEME, ONLY : I_NEQ=>NEQ, I_CNAMES=>CNAMES
USE MODD_CH_AEROSOL
USE MODD_DUST,         ONLY : LVARSIG, NMODE_DST, CDUSTNAMES, YPDUST_INI,&
                              JPDUSTORDER, LRGFIX_DST, CDEDSTNAMES, YPDEDST_INI
USE MODI_CH_INIT_CCS
USE MODI_CH_INIT_SCHEME


!**** *SUINI_NSV * - Setting up the boundaries of the scalar vector GFL_EXT

!     Purpose.
!     --------
!           Initialization of YOMNSV variables
!**   Interface.
!     ----------
!        *CALL* *AROINI_NSV0*

!        Explicit arguments :
!        --------------------

!        Implicit arguments :
!        --------------------
!           Common MODD_CH_M9
!           Common MODD_CH_AEROSOL

!     Method.
!     -------

!     Externals.
!     ----------

!     Reference.
!     ----------
!        

!     Author.
!     -------
!        P. Tulet  *CNRM*

!     Modifications.
!     --------------

IMPLICIT NONE

LOGICAL,INTENT(IN):: OORILAM
LOGICAL,INTENT(IN):: ODUST
LOGICAL,INTENT(IN):: OCO2
LOGICAL,INTENT(IN):: OUSECHEM
LOGICAL,INTENT(IN):: ODEPOS
INTEGER,INTENT(IN) ::KGFL 
INTEGER,INTENT(INOUT) ::KGFL_EXT 
INTEGER,INTENT(INOUT) ::KSV_CHEMBEG
INTEGER,INTENT(INOUT) ::KSV_CHEMEND
INTEGER,INTENT(INOUT) ::KSV_DSTBEG
INTEGER,INTENT(INOUT) ::KSV_DSTEND
INTEGER,INTENT(INOUT) ::KSV_DSTDEPBEG
INTEGER,INTENT(INOUT) ::KSV_DSTDEPEND
INTEGER,INTENT(INOUT) ::KSV_AERBEG
INTEGER,INTENT(INOUT) ::KSV_AEREND
INTEGER,INTENT(INOUT) ::KSV_CO2
CHARACTER(LEN=15), DIMENSION(KGFL),  INTENT(INOUT) :: CLNAME

INTEGER :: ISV ! total number of scalar variables
INTEGER :: JN, JSV, JSV_NAME, JMOMENTS, JMODE, JMODEIDX, JMOM ! index
INTEGER :: I_NM6_AER ! number of M6 aerosol variables
INTEGER :: I_NM6_DST ! number of M6 dust variables
INTEGER :: IMODEIDX
CHARACTER(LEN=10) :: CL_SCHEME

REAL(KIND=JPRB) :: ZHOOK_HANDLE
IF (LHOOK) CALL DR_HOOK('AROINI_NSV0',0,ZHOOK_HANDLE)
ISV = 0

IF (OUSECHEM) THEN

 NEQ    = I_NEQ
 CNAMES =>I_CNAMES
 CALL CH_INIT_SCHEME(6)
 CALL CH_INIT_CCS(NEQ,6,6)

 KSV_CHEMBEG = ISV+1
 KSV_CHEMEND = ISV + NEQ
 ISV = KSV_CHEMEND

 CL_SCHEME = "NONE" ! definition of gaseous scheme
  DO JN=1, SIZE(CNAMES)
   IF (TRIM(CNAMES(JN)) .EQ. "ALKA") CL_SCHEME = "RELACS"
   IF (TRIM(CNAMES(JN)) .EQ. "HC3")  CL_SCHEME = "RACM"
   IF (TRIM(CNAMES(JN)) .EQ. "URG1") CL_SCHEME = "RELACS2"
   IF (TRIM(CNAMES(JN)) .EQ. "UR21") CL_SCHEME = "CACM"
  ENDDO
DO JN=KSV_CHEMBEG,KSV_CHEMEND
CLNAME(JN) = TRIM(CNAMES(JN-KSV_CHEMBEG+1))
ENDDO
END IF


IF ((OUSECHEM).AND.(OORILAM)) THEN
 
 IF (CL_SCHEME == "RELACS2" .OR. CL_SCHEME == "CACM") THEN
    NSOA = 10
 ELSE
    NSOA = 0 ! No SOA formation
 END IF

 I_NM6_AER = 0
  IF (LVARSIGI) I_NM6_AER = 1
  IF (LVARSIGJ) I_NM6_AER = I_NM6_AER + 1
 KSV_AERBEG = ISV+1
 KSV_AEREND = ISV+(NSP+NCARB+NSOA+1)*JPMODE + I_NM6_AER
 ISV =  KSV_AEREND

 IF (ALLOCATED(CAERONAMES)) DEALLOCATE(CAERONAMES)
 ALLOCATE(CAERONAMES((NSP+NSOA+NCARB+1)*JPMODE+I_NM6_AER))
 ! Index gas scheme <=> Index Orilam
JP_CH_SO4I = 1
JP_CH_SO4J = 2
JP_CH_NO3I = 3
JP_CH_NO3J = 4
JP_CH_NH3I = 5
JP_CH_NH3J = 6
JP_CH_H2OI = 7
JP_CH_H2OJ = 8
JP_CH_OCI  = 9
JP_CH_OCJ  = 10
JP_CH_BCI  = 11
JP_CH_BCJ  = 12

JP_CH_M0I = (NCARB + NSP + NSOA)*JPMODE +1
JP_CH_M0J = (NCARB + NSP + NSOA)*JPMODE +2
JP_CH_M6I = 0
JP_CH_M6J = 0
IF (LVARSIGI) JP_CH_M6I = (NCARB + NSP + NSOA)*JPMODE + 3
IF ((LVARSIGI).AND.(LVARSIGJ)) THEN
   JP_CH_M6J = (NCARB + NSP + NSOA)*JPMODE + 4
ELSE IF ((LVARSIGJ).AND. .NOT.(LVARSIGI)) THEN
   JP_CH_M6J = (NCARB + NSP + NSOA)*JPMODE + 3
END IF

    ! Name of aerosol component
CAERONAMES(JP_CH_SO4I) = "SO4I"
CAERONAMES(JP_CH_SO4J) = "SO4J"
CAERONAMES(JP_CH_NH3I) = "NH3I"
CAERONAMES(JP_CH_NH3J) = "NH3J"
CAERONAMES(JP_CH_H2OI) = "H2OI"
CAERONAMES(JP_CH_H2OJ) = "H2OJ"
CAERONAMES(JP_CH_NO3I) = "NO3I"
CAERONAMES(JP_CH_NO3J) = "NO3J"
CAERONAMES(JP_CH_BCI) = "BCI"
CAERONAMES(JP_CH_BCJ) = "BCJ"
CAERONAMES(JP_CH_OCI) = "OCI"
CAERONAMES(JP_CH_OCJ) = "OCJ"
CAERONAMES(JP_CH_M0I) = "M0I"
CAERONAMES(JP_CH_M0J) = "M0J"
IF (LVARSIGI) CAERONAMES(JP_CH_M6I) = "M6I"
IF (LVARSIGJ) CAERONAMES(JP_CH_M6J) = "M6J"

IF (NSOA .EQ. 10) THEN
JP_CH_SOA1I = 13
JP_CH_SOA1J = 14
JP_CH_SOA2I = 15
JP_CH_SOA2J = 16
JP_CH_SOA3I = 17
JP_CH_SOA3J = 18
JP_CH_SOA4I = 19
JP_CH_SOA4J = 20
JP_CH_SOA5I = 21
JP_CH_SOA5J = 22
JP_CH_SOA6I = 23
JP_CH_SOA6J = 24
JP_CH_SOA7I = 25
JP_CH_SOA7J = 26
JP_CH_SOA8I = 27
JP_CH_SOA8J = 28
JP_CH_SOA9I = 29
JP_CH_SOA9J = 30
JP_CH_SOA10I = 31
JP_CH_SOA10J = 32
CAERONAMES(JP_CH_SOA1I) = "SOA1I"
CAERONAMES(JP_CH_SOA1J) = "SOA1J"
CAERONAMES(JP_CH_SOA2I) = "SOA2I"
CAERONAMES(JP_CH_SOA2J) = "SOA2J"
CAERONAMES(JP_CH_SOA3I) = "SOA3I"
CAERONAMES(JP_CH_SOA3J) = "SOA3J"
CAERONAMES(JP_CH_SOA4I) = "SOA4I"
CAERONAMES(JP_CH_SOA4J) = "SOA4J"
CAERONAMES(JP_CH_SOA5I) = "SOA5I"
CAERONAMES(JP_CH_SOA5J) = "SOA5J"
CAERONAMES(JP_CH_SOA6I) = "SOA6I"
CAERONAMES(JP_CH_SOA6J) = "SOA6J"
CAERONAMES(JP_CH_SOA7I) = "SOA7I"
CAERONAMES(JP_CH_SOA7J) = "SOA7J"
CAERONAMES(JP_CH_SOA8I) = "SOA8I"
CAERONAMES(JP_CH_SOA8J) = "SOA8J"
CAERONAMES(JP_CH_SOA9I) = "SOA9I"
CAERONAMES(JP_CH_SOA9J) = "SOA9J"
CAERONAMES(JP_CH_SOA10I) = "SOA10I"
CAERONAMES(JP_CH_SOA10J) = "SOA10J"
END IF

DO JN=KSV_AERBEG,KSV_AEREND
CLNAME(JN) = TRIM(CAERONAMES(JN-KSV_AERBEG+1))
ENDDO
END IF

KSV_CO2 = 0
IF (OCO2) THEN
 KSV_CO2 = ISV+1
 ISV = KSV_CO2
 CLNAME(KSV_CO2) = "CO2INT"
END IF


IF (ODUST) THEN
 I_NM6_DST = 0
 IF (ODEPOS) LRGFIX_DST = .TRUE.
 IF (LRGFIX_DST) LVARSIG = .FALSE.
 IF (LVARSIG) I_NM6_DST = 1
 KSV_DSTBEG = ISV+1
 IF (LRGFIX_DST) THEN
   KSV_DSTEND = ISV+NMODE_DST
 ELSE
   KSV_DSTEND = ISV+NMODE_DST*(2+I_NM6_DST)
 ENDIF
 
 ISV = KSV_DSTEND 

  IF(.NOT.ALLOCATED(CDUSTNAMES)) THEN
     JMOMENTS = (KSV_DSTEND - KSV_DSTBEG +1 )/NMODE_DST
     ALLOCATE(CDUSTNAMES(JMOMENTS*NMODE_DST))
     !Loop on all dust modes
     IF (JMOMENTS == 1) THEN
        DO JMODE=1,NMODE_DST
        IMODEIDX=JPDUSTORDER(JMODE)
        JSV_NAME = (IMODEIDX - 1)*3 + 2
        CDUSTNAMES(JMODE) = YPDUST_INI(JSV_NAME)
        END DO
    ELSE

     DO JMODE=1,NMODE_DST  
        !Find which mode we are dealing with
        JMODEIDX=JPDUSTORDER(JMODE)
        DO JMOM=1,JMOMENTS
           !Find which number this is of the list of scalars
           JSV = (JMODE-1)*JMOMENTS + JMOM
           !Find what name this corresponds to, always 3 moments assumed in YPDUST_INI
           JSV_NAME = (JMODEIDX - 1)*3 + JMOM
           !Get the right CDUSTNAMES which should follow the list of scalars transported in XSVM/XSVT
           CDUSTNAMES(JSV) = YPDUST_INI(JSV_NAME)
        ENDDO ! Loop on moments
     ENDDO    ! Loop on dust modes
    END IF   
  END IF      !Check on allocated CDUSTNAMES already

DO JN=KSV_DSTBEG,KSV_DSTEND
CLNAME(JN) = TRIM(CDUSTNAMES(JN-KSV_DSTBEG+1))
ENDDO

IF ((ODUST).AND.(ODEPOS)) THEN
 KSV_DSTDEPBEG = ISV+1
 KSV_DSTDEPEND = ISV+NMODE_DST*2
 IF(.NOT.ALLOCATED(CDEDSTNAMES)) THEN
  ALLOCATE(CDEDSTNAMES(NMODE_DST*2))
  DO JN=1,NMODE_DST  
     JMODEIDX=JPDUSTORDER(JN)
     CDEDSTNAMES(JN) = YPDEDST_INI(JMODEIDX)
     CDEDSTNAMES(NMODE_DST+JN) = YPDEDST_INI(NMODE_DST+JMODEIDX)
  ENDDO
 ENDIF  
DO JN=KSV_DSTDEPBEG,KSV_DSTDEPEND
 CLNAME(JN) = TRIM(CDEDSTNAMES(JN-KSV_DSTDEPBEG+1))
ENDDO
ENDIF

END IF

KGFL_EXT = ISV

IF (LHOOK) CALL DR_HOOK('AROINI_NSV0',1,ZHOOK_HANDLE)
END SUBROUTINE AROINI_NSV0
