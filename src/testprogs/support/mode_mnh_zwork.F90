MODULE MODE_MNH_ZWORK

  USE ISO_C_BINDING, ONLY: C_LOC, C_F_POINTER
  USE STACK_MOD,     ONLY: STACK, SOF

  IMPLICIT NONE

  REAL, SAVE, POINTER, DIMENSION(:,:)      :: ZMNH_STACK
  INTEGER, POINTER                         :: IMNH_BLOCK
  TYPE(STACK), POINTER                     :: YMNH_STACK
  INTEGER                                  :: INUMPIN
  INTEGER, DIMENSION(100)                  :: PINVAL
!$OMP THREADPRIVATE (IMNH_BLOCK, YMNH_STACK, INUMPIN, PINVAL)

  INTERFACE MNH_MEM_GET
    MODULE PROCEDURE :: MNH_ALLOCATE_GT1DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_GT1DFLAT_SIZE
    MODULE PROCEDURE :: MNH_ALLOCATE_GT2DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_GT2DFLAT_SIZE
    MODULE PROCEDURE :: MNH_ALLOCATE_GT3DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_GT3DFLAT_SIZE
    MODULE PROCEDURE :: MNH_ALLOCATE_IT1DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_IT1DFLAT_SIZE
    MODULE PROCEDURE :: MNH_ALLOCATE_IT2DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_IT2DFLAT_SIZE
    MODULE PROCEDURE :: MNH_ALLOCATE_IT3DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_IT3DFLAT_SIZE
    MODULE PROCEDURE :: MNH_ALLOCATE_ZT1DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_ZT1DFLAT_SIZE
    MODULE PROCEDURE :: MNH_ALLOCATE_ZT2DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_ZT2DFLAT_SIZE
    MODULE PROCEDURE :: MNH_ALLOCATE_ZT3DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_ZT3DFLAT_SIZE
    MODULE PROCEDURE :: MNH_ALLOCATE_ZT4DFLAT
    MODULE PROCEDURE :: MNH_ALLOCATE_ZT4DFLAT_SIZE
  END INTERFACE MNH_MEM_GET

CONTAINS

  SUBROUTINE MNH_ALLOCATE_GT1DFLAT(OTAB, KIB, KIE)

    LOGICAL, POINTER, CONTIGUOUS , DIMENSION(:), INTENT(INOUT) :: OTAB
    INTEGER,                                     INTENT(IN)    :: KIB
    INTEGER,                                     INTENT(IN)    :: KIE

    INTEGER :: ISIZE, IOFFSET

    IOFFSET = (KIB - 1)
    ISIZE = (KIE - KIB + 1)
    IOFFSET = CEILING(IOFFSET * REAL(KIND(OTAB)) / REAL(KIND(ZMNH_STACK)))
    ISIZE = CEILING(ISIZE * REAL(KIND(OTAB)) / REAL(KIND(ZMNH_STACK)))

    CALL C_F_POINTER(C_LOC(ZMNH_STACK(YMNH_STACK%L - IOFFSET, IMNH_BLOCK)), OTAB, [KIE - KIB + 1])
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_GT1DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_GT1DFLAT

  SUBROUTINE MNH_ALLOCATE_GT1DFLAT_SIZE(OTAB, KSIZE)

    LOGICAL, POINTER, CONTIGUOUS , DIMENSION(:), INTENT(INOUT) :: OTAB
    INTEGER,                                     INTENT(IN)    :: KSIZE

    CALL MNH_ALLOCATE_GT1DFLAT(OTAB, 1, KSIZE)

  END SUBROUTINE MNH_ALLOCATE_GT1DFLAT_SIZE

  SUBROUTINE MNH_ALLOCATE_GT2DFLAT(OTAB, KIB, KIE, KJB, KJE)

    LOGICAL, POINTER, CONTIGUOUS , DIMENSION(:,:), INTENT(INOUT) :: OTAB
    INTEGER,                                       INTENT(IN)    :: KIB
    INTEGER,                                       INTENT(IN)    :: KIE
    INTEGER,                                       INTENT(IN)    :: KJB
    INTEGER,                                       INTENT(IN)    :: KJE

    INTEGER :: ISIZE, IOFFSET

    IOFFSET = (KJB - 1) * (KIE - KIB + 1) + &
            & (KIB - 1)
    ISIZE = (KIE - KIB + 1) * (KJE - KJB + 1)
    IOFFSET = CEILING(IOFFSET * REAL(KIND(OTAB)) / REAL(KIND(ZMNH_STACK)))
    ISIZE = CEILING(ISIZE * REAL(KIND(OTAB)) / REAL(KIND(ZMNH_STACK)))

    CALL C_F_POINTER(C_LOC(ZMNH_STACK(YMNH_STACK%L - IOFFSET, IMNH_BLOCK)), OTAB, [KIE - KIB + 1, KJE - KJB + 1])
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_GT2DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_GT2DFLAT

  SUBROUTINE MNH_ALLOCATE_GT2DFLAT_SIZE(OTAB, KISIZE, KJSIZE)

    LOGICAL, POINTER, CONTIGUOUS , DIMENSION(:,:), INTENT(INOUT) :: OTAB
    INTEGER,                                       INTENT(IN)    :: KISIZE
    INTEGER,                                       INTENT(IN)    :: KJSIZE

    CALL MNH_ALLOCATE_GT2DFLAT(OTAB, 1, KISIZE, 1, KJSIZE)

  END SUBROUTINE MNH_ALLOCATE_GT2DFLAT_SIZE

  SUBROUTINE MNH_ALLOCATE_GT3DFLAT(OTAB, KIB, KIE, KJB, KJE, KKB, KKE)

    LOGICAL, POINTER, CONTIGUOUS , DIMENSION(:,:,:), INTENT(INOUT) :: OTAB
    INTEGER,                                         INTENT(IN)    :: KIB
    INTEGER,                                         INTENT(IN)    :: KIE
    INTEGER,                                         INTENT(IN)    :: KJB
    INTEGER,                                         INTENT(IN)    :: KJE
    INTEGER,                                         INTENT(IN)    :: KKB
    INTEGER,                                         INTENT(IN)    :: KKE

    INTEGER :: ISIZE, IOFFSET

    IOFFSET = (KKB - 1) * (KIE - KIB + 1) * (KJE - KJB + 1) + &
            & (KJB - 1) * (KIE - KIB + 1) + &
            & (KIB - 1)
    ISIZE = (KIE - KIB + 1) * (KJE - KJB + 1) * (KKE - KKB + 1)
    IOFFSET = CEILING(IOFFSET * REAL(KIND(OTAB)) / REAL(KIND(ZMNH_STACK)))
    ISIZE = CEILING(ISIZE * REAL(KIND(OTAB)) / REAL(KIND(ZMNH_STACK)))

    CALL C_F_POINTER(C_LOC(ZMNH_STACK(YMNH_STACK%L - IOFFSET, IMNH_BLOCK)), OTAB, [KIE - KIB + 1, KJE - KJB + 1, KKE - KKB + 1])
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_GT3DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_GT3DFLAT

  SUBROUTINE MNH_ALLOCATE_GT3DFLAT_SIZE(OTAB, KISIZE, KJSIZE, KKSIZE)

    LOGICAL, POINTER, CONTIGUOUS , DIMENSION(:,:,:), INTENT(INOUT) :: OTAB
    INTEGER,                                         INTENT(IN)    :: KISIZE
    INTEGER,                                         INTENT(IN)    :: KJSIZE
    INTEGER,                                         INTENT(IN)    :: KKSIZE

    CALL MNH_ALLOCATE_GT3DFLAT(OTAB, 1, KISIZE, 1, KJSIZE, 1, KKSIZE)

  END SUBROUTINE MNH_ALLOCATE_GT3DFLAT_SIZE

  SUBROUTINE MNH_ALLOCATE_IT1DFLAT(KTAB, KIB, KIE)

    INTEGER, POINTER, CONTIGUOUS , DIMENSION(:), INTENT(INOUT) :: KTAB
    INTEGER,                                     INTENT(IN)    :: KIB
    INTEGER,                                     INTENT(IN)    :: KIE

    INTEGER :: ISIZE, IOFFSET

    IOFFSET = (KIB - 1)
    ISIZE = (KIE - KIB + 1)
    IOFFSET = CEILING(IOFFSET * REAL(KIND(KTAB)) / REAL(KIND(ZMNH_STACK)))
    ISIZE = CEILING(ISIZE * REAL(KIND(KTAB)) / REAL(KIND(ZMNH_STACK)))

    CALL C_F_POINTER(C_LOC(ZMNH_STACK(YMNH_STACK%L - IOFFSET, IMNH_BLOCK)), KTAB, [KIE - KIB + 1])
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_IT1DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_IT1DFLAT

  SUBROUTINE MNH_ALLOCATE_IT1DFLAT_SIZE(KTAB, KSIZE)

    INTEGER, POINTER, CONTIGUOUS , DIMENSION(:), INTENT(INOUT) :: KTAB
    INTEGER,                                     INTENT(IN)    :: KSIZE

    CALL MNH_ALLOCATE_IT1DFLAT(KTAB, 1, KSIZE)

  END SUBROUTINE MNH_ALLOCATE_IT1DFLAT_SIZE

  SUBROUTINE MNH_ALLOCATE_IT2DFLAT(KTAB, KIB, KIE, KJB, KJE)

    INTEGER, POINTER, CONTIGUOUS , DIMENSION(:,:), INTENT(INOUT) :: KTAB
    INTEGER,                                       INTENT(IN)    :: KIB
    INTEGER,                                       INTENT(IN)    :: KIE
    INTEGER,                                       INTENT(IN)    :: KJB
    INTEGER,                                       INTENT(IN)    :: KJE

    INTEGER :: ISIZE, IOFFSET

    IOFFSET = (KJB - 1) * (KIE - KIB + 1) + &
            & (KIB - 1)
    ISIZE = (KIE - KIB + 1) * (KJE - KJB + 1)
    IOFFSET = CEILING(IOFFSET * REAL(KIND(KTAB)) / REAL(KIND(ZMNH_STACK)))   
    ISIZE = CEILING(ISIZE * REAL(KIND(KTAB)) / REAL(KIND(ZMNH_STACK)))

    CALL C_F_POINTER(C_LOC(ZMNH_STACK(YMNH_STACK%L - IOFFSET, IMNH_BLOCK)), KTAB, [KIE - KIB + 1, KJE - KJB + 1])
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_IT2DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_IT2DFLAT

  SUBROUTINE MNH_ALLOCATE_IT2DFLAT_SIZE(KTAB, KISIZE, KJSIZE)

    INTEGER, POINTER, CONTIGUOUS , DIMENSION(:,:), INTENT(INOUT) :: KTAB
    INTEGER,                                       INTENT(IN)    :: KISIZE
    INTEGER,                                       INTENT(IN)    :: KJSIZE

    CALL MNH_ALLOCATE_IT2DFLAT(KTAB, 1, KISIZE, 1, KJSIZE)

  END SUBROUTINE MNH_ALLOCATE_IT2DFLAT_SIZE

  SUBROUTINE MNH_ALLOCATE_IT3DFLAT(KTAB, KIB, KIE, KJB, KJE, KKB, KKE)

    INTEGER, POINTER, CONTIGUOUS , DIMENSION(:,:,:), INTENT(INOUT) :: KTAB
    INTEGER,                                         INTENT(IN)    :: KIB
    INTEGER,                                         INTENT(IN)    :: KIE
    INTEGER,                                         INTENT(IN)    :: KJB
    INTEGER,                                         INTENT(IN)    :: KJE
    INTEGER,                                         INTENT(IN)    :: KKB
    INTEGER,                                         INTENT(IN)    :: KKE

    INTEGER :: ISIZE, IOFFSET

    IOFFSET = (KKB - 1) * (KIE - KIB + 1) * (KJE - KJB + 1) + &
            & (KJB - 1) * (KIE - KIB + 1) + &
            & (KIB - 1)
    ISIZE = (KIE - KIB + 1) * (KJE - KJB + 1) * (KKE - KKB + 1)
    IOFFSET = CEILING(IOFFSET * REAL(KIND(KTAB)) / REAL(KIND(ZMNH_STACK)))   
    ISIZE = CEILING(ISIZE * REAL(KIND(KTAB)) / REAL(KIND(ZMNH_STACK)))

    CALL C_F_POINTER(C_LOC(ZMNH_STACK(YMNH_STACK%L - IOFFSET, IMNH_BLOCK)), KTAB, [KIE - KIB + 1, KJE - KJB + 1, KKE - KKB + 1])
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_IT3DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_IT3DFLAT

  SUBROUTINE MNH_ALLOCATE_IT3DFLAT_SIZE(KTAB, KISIZE, KJSIZE, KKSIZE)

    INTEGER, POINTER, CONTIGUOUS , DIMENSION(:,:,:), INTENT(INOUT) :: KTAB
    INTEGER,                                         INTENT(IN)    :: KISIZE
    INTEGER,                                         INTENT(IN)    :: KJSIZE
    INTEGER,                                         INTENT(IN)    :: KKSIZE

    CALL MNH_ALLOCATE_IT3DFLAT(KTAB, 1, KISIZE, 1, KJSIZE, 1, KKSIZE)

  END SUBROUTINE MNH_ALLOCATE_IT3DFLAT_SIZE

  SUBROUTINE MNH_ALLOCATE_ZT1DFLAT(PTAB, KIB, KIE)

    REAL, POINTER, CONTIGUOUS , DIMENSION(:), INTENT(INOUT) :: PTAB
    INTEGER,                                  INTENT(IN)    :: KIB
    INTEGER,                                  INTENT(IN)    :: KIE

    INTEGER :: ISIZE

    ISIZE = (KIE - KIB + 1)

    PTAB(KIB:KIE) => ZMNH_STACK(YMNH_STACK%L:YMNH_STACK%L+ISIZE-1, IMNH_BLOCK)
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_ZT1DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_ZT1DFLAT

  SUBROUTINE MNH_ALLOCATE_ZT1DFLAT_SIZE(PTAB, KSIZE)

    REAL, POINTER, CONTIGUOUS , DIMENSION(:), INTENT(INOUT) :: PTAB
    INTEGER,                                  INTENT(IN)    :: KSIZE

    CALL MNH_ALLOCATE_ZT1DFLAT(PTAB, 1, KSIZE)

  END SUBROUTINE MNH_ALLOCATE_ZT1DFLAT_SIZE

  SUBROUTINE MNH_ALLOCATE_ZT2DFLAT(PTAB, KIB, KIE, KJB, KJE)

    REAL, POINTER, CONTIGUOUS , DIMENSION(:,:), INTENT(INOUT) :: PTAB
    INTEGER,                                    INTENT(IN)    :: KIB
    INTEGER,                                    INTENT(IN)    :: KIE
    INTEGER,                                    INTENT(IN)    :: KJB
    INTEGER,                                    INTENT(IN)    :: KJE

    INTEGER :: ISIZE

    ISIZE = (KIE - KIB + 1) * (KJE - KJB + 1)

    PTAB(KIB:KIE, KJB:KJE) => ZMNH_STACK(YMNH_STACK%L:YMNH_STACK%L+ISIZE-1, IMNH_BLOCK)
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_ZT2DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_ZT2DFLAT

  SUBROUTINE MNH_ALLOCATE_ZT2DFLAT_SIZE(PTAB, KISIZE, KJSIZE)

    REAL, POINTER, CONTIGUOUS , DIMENSION(:,:), INTENT(INOUT) :: PTAB
    INTEGER,                                    INTENT(IN)    :: KISIZE
    INTEGER,                                    INTENT(IN)    :: KJSIZE

    CALL MNH_ALLOCATE_ZT2DFLAT(PTAB, 1, KISIZE, 1, KJSIZE)

  END SUBROUTINE MNH_ALLOCATE_ZT2DFLAT_SIZE

  SUBROUTINE MNH_ALLOCATE_ZT3DFLAT(PTAB, KIB, KIE, KJB, KJE, KKB, KKE)

    REAL, POINTER, CONTIGUOUS , DIMENSION(:,:,:), INTENT(INOUT) :: PTAB
    INTEGER,                                      INTENT(IN)    :: KIB
    INTEGER,                                      INTENT(IN)    :: KIE
    INTEGER,                                      INTENT(IN)    :: KJB
    INTEGER,                                      INTENT(IN)    :: KJE
    INTEGER,                                      INTENT(IN)    :: KKB
    INTEGER,                                      INTENT(IN)    :: KKE

    INTEGER :: ISIZE

    ISIZE = (KIE - KIB + 1) * (KJE - KJB + 1) * (KKE - KKB + 1)

    PTAB(KIB:KIE, KJB:KJE, KKB:KKE) => ZMNH_STACK(YMNH_STACK%L:YMNH_STACK%L+ISIZE-1, IMNH_BLOCK)                    
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_ZT3DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_ZT3DFLAT

  SUBROUTINE MNH_ALLOCATE_ZT3DFLAT_SIZE(PTAB, KISIZE, KJSIZE, KKSIZE)

    REAL, POINTER, CONTIGUOUS , DIMENSION(:,:,:), INTENT(INOUT) :: PTAB
    INTEGER,                                      INTENT(IN)    :: KISIZE
    INTEGER,                                      INTENT(IN)    :: KJSIZE
    INTEGER,                                      INTENT(IN)    :: KKSIZE

    CALL MNH_ALLOCATE_ZT3DFLAT(PTAB, 1, KISIZE, 1, KJSIZE, 1, KKSIZE)

  END SUBROUTINE MNH_ALLOCATE_ZT3DFLAT_SIZE

  SUBROUTINE MNH_ALLOCATE_ZT4DFLAT(PTAB, KIB, KIE, KJB, KJE, KKB, KKE, KPB, KPE)

    REAL, POINTER, CONTIGUOUS , DIMENSION(:,:,:,:), INTENT(INOUT) :: PTAB
    INTEGER,                                        INTENT(IN)    :: KIB
    INTEGER,                                        INTENT(IN)    :: KIE
    INTEGER,                                        INTENT(IN)    :: KJB
    INTEGER,                                        INTENT(IN)    :: KJE
    INTEGER,                                        INTENT(IN)    :: KKB
    INTEGER,                                        INTENT(IN)    :: KKE
    INTEGER,                                        INTENT(IN)    :: KPB
    INTEGER,                                        INTENT(IN)    :: KPE

    INTEGER :: ISIZE

    ISIZE = (KIE - KIB + 1) * (KJE - KJB + 1) * (KKE - KKB + 1) * (KPE - KPB + 1)

    PTAB(KIB:KIE, KJB:KJE, KKB:KKE, KPB:KPE) => ZMNH_STACK(YMNH_STACK%L:YMNH_STACK%L+ISIZE-1, IMNH_BLOCK)
    YMNH_STACK%L=YMNH_STACK%L+ISIZE
    IF(YMNH_STACK%L>YMNH_STACK%U) CALL SOF('MNH_ALLOCATE_ZT4DFLAT', 0)

  END SUBROUTINE MNH_ALLOCATE_ZT4DFLAT

  SUBROUTINE MNH_ALLOCATE_ZT4DFLAT_SIZE(PTAB, KISIZE, KJSIZE, KKSIZE, KPSIZE)

    REAL, POINTER, CONTIGUOUS , DIMENSION(:,:,:,:), INTENT(INOUT) :: PTAB
    INTEGER,                                        INTENT(IN)    :: KISIZE
    INTEGER,                                        INTENT(IN)    :: KJSIZE
    INTEGER,                                        INTENT(IN)    :: KKSIZE
    INTEGER,                                        INTENT(IN)    :: KPSIZE

    CALL MNH_ALLOCATE_ZT4DFLAT(PTAB, 1, KISIZE, 1, KJSIZE, 1, KKSIZE, 1, KPSIZE)

  END SUBROUTINE MNH_ALLOCATE_ZT4DFLAT_SIZE

  SUBROUTINE MNH_MEM_POSITION_PIN(HSUBR)
    CHARACTER(LEN=*), OPTIONAL, INTENT(IN) :: HSUBR !NAME OF THE CALLING SUBROUTINE

    INUMPIN = INUMPIN + 1
    IF(INUMPIN>SIZE(PINVAL)) THEN
        PRINT*, "PINVAL too small"
        STOP 2
    ENDIF
    PINVAL(INUMPIN) = YMNH_STACK%L
  END SUBROUTINE MNH_MEM_POSITION_PIN

  SUBROUTINE MNH_MEM_RELEASE(HSUBR)
    CHARACTER(LEN=*), OPTIONAL, INTENT(IN) :: HSUBR !NAME OF THE CALLING SUBROUTINE

    YMNH_STACK%L = PINVAL(INUMPIN)
    INUMPIN = INUMPIN - 1
  END SUBROUTINE MNH_MEM_RELEASE

END MODULE MODE_MNH_ZWORK
